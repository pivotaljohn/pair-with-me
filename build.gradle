// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

allprojects {
    repositories {
        jcenter()
    }

    //noinspection GroovyAssignabilityCheck -- the following works; not sure what the warning is about.
    configurations.all {
        resolutionStrategy {
            /*
             "Use latest" strategy means risking
             1. fail fast and;
             2. resolve conflicts using "force()", below.
             */
            failOnVersionConflict()

            /*
             TL;DR:
             Because the Test APK and App APK share a classpath, its easy for a dependency from
             "compile" and "test" to conflict. You can either detect and address each version
             conflict one-by-one, or fix the version for the whole project.

             Details:
             https://sites.google.com/a/android.com/tools/tech-docs/new-build-system/user-guide#TOC-Resolving-conflicts-between-main-and-test-APK
           */
            force "com.android.support:support-annotations:$rootProject.ANDROID_SUPPORT_LIBRARY_VER"
        }
    }
}

ext {
    APPLICATION_ID = 'io.pivotal.pairwithme'
    TEST_APPLICATION_ID = APPLICATION_ID + '.test'
    VERSION_CODE = 1
    VERSION_NAME = '0.0.1'

    ANDROID_SDK_VER = 23
    MIN_SDK_VER = 15                     // https://developer.android.com/about/dashboards/index.html
    ANDROID_BUILD_TOOLS_VER = '23.0.3'   // https://developer.android.com/tools/revisions/build-tools.html

    // Core App Libraries
    ANDROID_SUPPORT_LIBRARY_VER = '23.4.0' // https://developer.android.com/tools/support-library/index.html#revisions

    // Testing
    JUNIT_VER = '4.12'  // https://github.com/junit-team/junit4/releases/latest

    // Instrumented Testing
    // https://google.github.io/android-testing-support-library/docs/espresso/setup/
    ESPRESSO_VER = '2.2.2'
    ANDROID_TEST_SUPPORT_LIBRARY_VER = '0.5'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
